subroutine Interaktion_Auftragsdurchfuehrung()
	for ni = 0 to Nachfrage_Anzahl - 1
		ri = Nachfrage_RaketeIndex[ni]
		if ri <> -1 then
			spi = Rakete_SpielerIndex[ri]
			istNutzlastErfolg = false
			istLandungErfolg = false
			nai = Nachfrage_ArtIndex[ni]
			zuverlaessigkeit = Rakete_ZuverlaessigkeitFuerNachfrageart(ri, nai)
			wiederverwendbarkeit = Rakete_WiederverwendbarkeitFuerNachfrageart(ri, nai)
			kosten = Rakete_Kosten(ri)
			reuseWert = kosten - Rakete_KostenInstandsetzung(ri)
			gebot = Nachfrage_SiegGebot[ni]
			cls
			print Spieler_Name$[spi] + " bereitet Start von " + Raketenart_Name$[Rakete_ArtIndex[ri]] + " fuer " + Nachfrageart_Name$[Nachfrage_ArtIndex[ni]] + " vor..."
			call Technik_Echo("Rakete kostete " + kosten + "M")
			call Technik_Echo("Die Rakete startet...")
			if rand > zuverlaessigkeit then
				call Technik_Echo("Rakete ist explodiert...")
				call Technik_Echo("Strafzahlung von " + Nachfrageart_Strafe[nai] + "M...")
			else
				istNutzlastErfolg = true
				call Technik_Echo(Nachfrageart_Name$[nai] + " ist am Ziel angekommen...")
				call Technik_Echo("Praemie von " + gebot + "M erhalten...")
				call Technik_Echo("Erste Raketenstufe bereit zur Landung...")
				if rand > wiederverwendbarkeit then
					call Technik_Echo("Explosion...")
				else
					istLandungErfolg = true
					call Technik_Echo("Landung erfolgreich.")
					call Technik_Echo("Rakete im Wert von " + reuseWert + "M zurueckbekommen...")
				endif
			endif
			call Spieler_Auftragsergebnis(ni, istNutzlastErfolg, istLandungErfolg)
			input a$
		endif
	next ni
end subroutine
