subroutine Interaktion_Auftragsvergabe()
	Nachfrage_Anzahl = Nachfrage_AnzahlVergeben() + 1
	if Nachfrage_Anzahl > NACHFRAGE_MAX_ANZAHL then
		Nachfrage_Anzahl = NACHFRAGE_MAX_ANZAHL
	endif
	for ni = 0 to NACHFRAGE_MAX_ANZAHL - 1
		Nachfrage_RaketeIndex[ni] = -1
	next ni
	for ni = 0 to Nachfrage_Anzahl - 1
		nai = int(rand * Nachfrageart_VerfuegbarAnzahl)
		Nachfrage_ArtIndex[ni] = nai
		call Interaktion_Auktion(ni)
	next ni
end subroutine

subroutine Interaktion_Auktion(ni)
	nai = Nachfrage_ArtIndex[ni]
	dim spielerGebotRakete(Spieler_Anzahl)
	zugelasseneSpielerAnzahl = 0
	for spi = 0 to Spieler_Anzahl - 1
		ri = Spieler_IndexBesteRaketeFuerNachfragenart(spi, nai)
		if ri <> -1 then
			call Spieler_AuktionTeilnehmen(spi)
			zugelasseneSpielerAnzahl++
		endif
		spielerGebotRakete[spi] = ri
	next spi
	gebot = Nachfrageart_Anfangsgebot[nai]
	aktuellGebotSpielerIndex = -1
	uebrigeTicks = 1
	while uebrigeTicks > 0
		uebrigeTicks = GEBOT_MAXZEIT / 0.1
		call Interaktion_GebotUebersicht(nai, ref(spielerGebotRakete))
		print
		if aktuellGebotSpielerIndex = -1 then
			print "Anfangsgebot: ";
		else
			print Spieler_Name$[aktuellGebotSpielerIndex] + " Gebot: ";
		endif
		print "" + gebot + "M"
		if aktuellGebotSpielerIndex = -1 then
			print
			if zugelasseneSpielerAnzahl = 0 then
				a$ = Technik_Input$("Keiner kann eine Rakete zum Start bringen...")
				uebrigeTicks = 0
			else
				a$ = Technik_Input$("Bei Return geht's los")
				print "...Start!";
			endif
		endif
		gebotSpielerIndex = -1
		while gebotSpielerIndex = -1 and uebrigeTicks > 0
			pause 0.1
			a = key
			gebotSpielerIndex = Spieler_IndexFuerKey(a)
			if aktuellGebotSpielerIndex = gebotSpielerIndex then
				gebotSpielerIndex = -1
			endif
			if gebotSpielerIndex <> -1 then
				if spielerGebotRakete[gebotSpielerIndex] = -1 then
					gebotSpielerIndex = -1
				endif
			endif
			uebrigeTicks--
			if uebrigeTicks % 10 = 0 then
				print "..." + (uebrigeTicks / 10);
			endif
		end while
		if gebotSpielerIndex <> -1 then
			if aktuellGebotSpielerIndex <> -1 then
				gebot -= GEBOT_INTERVALL
			endif
			aktuellGebotSpielerIndex = gebotSpielerIndex
			call Spieler_GebotAbgeben(aktuellGebotSpielerIndex)
		endif
	end while
	if aktuellGebotSpielerIndex <> -1 then
		call Nachfrage_RaketeSieggebot(ni, gebot, spielerGebotRakete[aktuellGebotSpielerIndex])
	else
		call Nachfrage_KeinSieggebot(ni)
	endif
end subroutine

subroutine Interaktion_GebotUebersicht(nai, ref(spielerGebotRakete))
	cls
	print "Nachfrage " + Nachfrageart_Name$[nai]
	print "====================="
	print
	print "Notwendige Subkraft: " + Nachfrageart_BenoetigteSchubkraft[nai] + " MN"
	print "Strafe bei Fehler: " + Nachfrageart_Strafe[nai] + "M"
	print
	for spi = 0 to Spieler_Anzahl - 1
		if spielerGebotRakete[spi] <> -1 then
			print Rakete_TextFuerNachfrageart$(spielerGebotRakete[spi], nai)
		endif
	next spi
end subroutine
